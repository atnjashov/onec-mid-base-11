//Атняшов В. А.: Механизмы печати

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Настраивает печать
// 
// Параметры:
// НастройкиОбъекта - Структура
Процедура ПриОпределенииНастроекПечати(НастройкиОбъекта) Экспорт	
	НастройкиОбъекта.ПриДобавленииКомандПечати = Истина;
КонецПроцедуры

// Добавляет КомандыПечати
// 
// Параметры:
// КомандыПечати - Структура
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Анкета клиента
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АнкетаКлиента";
	КомандаПечати.Представление = НСтр("ru = 'Анкета клиента'");
	КомандаПечати.Порядок = 20;
	
	// Товарная накладная
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТоварнаяНакладная";
	КомандаПечати.Представление = НСтр("ru = 'Товарная накладная'");
	КомандаПечати.Порядок = 40;
	
	// Комплект документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АнкетаКлиента,ТоварнаяНакладная";
	КомандаПечати.Представление = НСтр("ru = 'Комплект документов'");
	КомандаПечати.Порядок = 60;
	
	// Договор на доставку
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "УправлениеПечатью";
	КомандаПечати.Идентификатор = "Документ.Доработка_Доставка.ПФ_DOC_Договор_На_Доставку";
	КомандаПечати.Представление = НСтр("ru = 'Договор на доставку'");
	КомандаПечати.Порядок = 80;
	
КонецПроцедуры

// Формирует печатный документ
// 
// Параметры:
// МассивОбъектов - Массив из Структура
// ПараметрыПечати - Структура
// КоллекцияПечатныхФорм - Массив из Форма
// ОбъектыПечати - Структура
// ПараметрыВывода - Булево
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "АнкетаКлиента");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьАнкетаКлиента(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Анкета клиента'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.Доработка_Доставка.ПФ_MXL_Анкета_Клиента";
	КонецЕсли;
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "ТоварнаяНакладная");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьТоварнаяНакладная(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Товарная накладная'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.Доработка_Доставка.ПФ_MXL_Товарная_Накладная";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПечатьАнкетаКлиента(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	МакетПечатнойФормы = "Документ.Доработка_Доставка.ПФ_MXL_Анкета_Клиента";
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_АнкетаКлиента";	
	
	ВыводДокументов(МассивОбъектов, ОбъектыПечати, УправлениеПечатью.МакетПечатнойФормы(МакетПечатнойФормы),
		ТабличныйДокумент, МакетПечатнойФормы);

	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПечатьТоварнаяНакладная(МассивОбъектов, ОбъектыПечати)

	ТабличныйДокумент = Новый ТабличныйДокумент;
	МакетПечатнойФормы = "Документ.Доработка_Доставка.ПФ_MXL_Товарная_Накладная";
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ТоварнаяНакладная";
	
	ВыводДокументов(МассивОбъектов, ОбъектыПечати, УправлениеПечатью.МакетПечатнойФормы(МакетПечатнойФормы),
		ТабличныйДокумент, МакетПечатнойФормы);
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Процедура ВыводДокументов(МассивОбъектов, ОбъектыПечати, Макет, ТабличныйДокумент, МакетПечатнойФормы)
	
	Накладная = МакетПечатнойФормы = "Документ.Доработка_Доставка.ПФ_MXL_Товарная_Накладная";
	Если Накладная Тогда
		ОбластьМакета = "ТекстТоварнаяНакладная_1";
		Заголовок = "ТранспортнаяНакладная";
		ТекстЗаголовка = "Транспортная накладная";
	Иначе
		ОбластьМакета = "ТекстАнкеты";	
		Заголовок = "АнкетаОДоставке";
		ТекстЗаголовка = "Анкета о доставке";
	КонецЕсли;

	ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов, Накладная); 

	ПервыйДокумент = Истина;
		
	Пока ДанныеДокументов.Следующий() Цикл
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		ДанныеПечати = Новый Структура;
		
		ОбластьТекстДокумента = Макет.ПолучитьОбласть(ОбластьМакета);
		ДанныеПечати.Вставить(Заголовок, СтрШаблон("%1 № %2 от %3 г.", ТекстЗаголовка,
			ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокументов.Номер),
			Формат(ДанныеДокументов.Дата, "ДФ=dd.MM.yyyy")));

		Если Накладная Тогда			
			ДанныеПечати.Вставить("Грузоотправитель", СтрШаблон("Грузоотправитель: %1", ДанныеДокументов.Организация));
			ДанныеПечати.Вставить("Грузополучатель", СтрШаблон("Грузополучатель: %1", ДанныеДокументов.Контрагент));
		КонецЕсли;
			
		ОбластьТекстДокумента.Параметры.Заполнить(ДанныеПечати);
		ТабличныйДокумент.Вывести(ОбластьТекстДокумента);

		Если Накладная Тогда			
			ОбластьСтрока = Макет.ПолучитьОбласть("ТоварСтрока");
			ВыборкаТовары = ДанныеДокументов.Товары.Выбрать();
			Пока ВыборкаТовары.Следующий() Цикл
				ОбластьСтрока.Параметры.Заполнить(ВыборкаТовары);
				ТабличныйДокумент.Вывести(ОбластьСтрока);
			КонецЦикла;
			ТабличныйДокумент.Вывести(Макет.ПолучитьОбласть("ТекстТоварнаяНакладная_2"));
		КонецЕсли;

	КонецЦикла;	

	ДанныеQRКода = ГенерацияШтрихкода.ДанныеQRКода(МакетПечатнойФормы, 1, 120);
	Если ТипЗнч(ДанныеQRКода) = Тип("ДвоичныеДанные") Тогда
		ТабличныйДокумент.Рисунки.QR_код.Картинка = Новый Картинка(ДанныеQRКода);
	Иначе
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не удалось сформировать QR-код.
		|Технические подробности см. в журнале регистрации.'"));
	КонецЕсли;
	
	    // В табличном документе необходимо задать имя области, в которую был 
	    // выведен объект. Нужно для возможности печати комплектов документов.
	УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
		НомерСтрокиНачало, ОбъектыПечати, ДанныеДокументов.Ссылка);		

КонецПроцедуры

Функция ПолучитьДанныеДокументов(МассивОбъектов, Накладная)
	
	Если Накладная Тогда
		Добавка = ",
	               |	Доставка.Организация КАК Организация,
	               |	Доставка.Контрагент КАК Контрагент,
	               |	Доставка.Товары.(
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		Количество КАК Количество
	               |	) КАК Товары";
	Иначе
		Добавка = "";
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрШаблон("ВЫБРАТЬ
	               |	Доставка.Ссылка КАК Ссылка,
				   |	Доставка.Номер КАК Номер,
	               |	Доставка.Дата КАК Дата%1
	               |ИЗ
	               |	Документ.Доработка_Доставка КАК Доставка
				   |ГДЕ
	               |	Доставка.Ссылка В(&МассивОбъектов)", Добавка);
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

#КонецОбласти

#КонецЕсли
// 